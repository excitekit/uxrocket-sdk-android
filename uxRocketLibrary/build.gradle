plugins {
    id 'kotlinx-serialization'
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

ext {
    lifecycleVersion = "2.4.1"
    coroutinesVersion = "1.5.2"
    okhttp3Version = '4.9.1'
    roomVersion = "2.4.1"
    retrofit2Version = "2.9.0"
    koinVersion = "3.2.0"
    koinFragmentVersion = "2.2.0"
    serializationVersion = "1.4.0-RC"
    dataStoreVersion = "1.0.0"
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "1.0.0"
}

def getGroupId = { ->
    return "com.github.martik1995"
}

def getArtificatId = { ->
    return "uxRocketLibrary"
}

publishing {
    repositories {
        maven {
            name = "LibPackages"
            url = uri("https://github.com/Martik1995/temp")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")

                println "GitHubPackages build.gradle\n\tusername=$username\n\ttoken=$password"
            }
        }
    }

    publications {
        bar(MavenPublication) {
            groupId getGroupId()
            artifactId getArtificatId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
            //generate .pom file with transitive dependencies

            pom.withXml {
                final dependenciesNode = asNode().appendNode('dependencies')
                ext.addDependency = { Dependency dep, String scope ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return
                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', scope)
                    if (!dep.transitive) {
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }
                configurations.implementation.getDependencies().each { dep -> addDependency(dep, "compile") }
                configurations.api.getDependencies().each { dep -> addDependency(dep, "compile") }
                configurations.implementation.getDependencies().each { dep -> addDependency(dep, "runtime") }
            }

        }
    }
}

android {
    compileSdk 32
    defaultConfig {
        minSdk 22
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    api 'joda-time:joda-time:2.10.14'
    api 'androidx.core:core-ktx:1.8.0'

    //Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // Koin for Android
    api "io.insert-koin:koin-core:$koinVersion"
    api "io.insert-koin:koin-android:$koinVersion"

    //Retrofit2
    api "com.squareup.retrofit2:retrofit:$retrofit2Version"
    api "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    //Serialization
    api "org.jetbrains.kotlinx:kotlinx-serialization-json:$serializationVersion"

    //OkHttp3
    api "com.squareup.okhttp3:okhttp:$okhttp3Version"
    api "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"

    //Annotation
    api 'javax.annotation:javax.annotation-api:1.3.2'

    //Room
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    //Coil
    api 'io.coil-kt:coil:1.1.1'
}

kapt {
    correctErrorTypes = true
}